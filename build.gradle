// Apply Gradle plugins
plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'idea'
    id 'checkstyle'

    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.github.johnrengelman.shadow' version '1.2.0'
}

defaultTasks 'clean', 'licenseFormat', 'build'

// Project information
group = 'net.caseif.beret'
version = '1.0.0-SNAPSHOT'

// Extended project information
ext.projectName = 'beret'
ext.description = 'Beret Extraordinary Reverse Engineering Toolkit'
ext.inceptionYear = '2015'
ext.packaging = 'jar'

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    deployerJars
}


// Project repositories
repositories {
    mavenCentral()
}

// Project dependencies
dependencies {
    testCompile 'junit:junit:4.12'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.8'
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'net.caseif.beret.Beret',
        )
    }
}

processResources {
    from 'LICENSE'
}

// License header formatting
license {
    header file('LICENSE')
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

// check code style
checkstyle {
    configProperties = [
            'name'		: project.name,
            'year'		: project.inceptionYear
    ]
    configFile = file('etc/checkstyle.xml')
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        setUniqueVersion(true)
        configuration = configurations.deployerJars
        if (project.hasProperty('mavenRepo')) {
            repository(url: project.property('mavenRepo')) {
                authentication(userName: project.property('mavenUser'), password: project.property('mavenPass'))
            }
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.ext.projectName
            project {
                packaging 'jar'
                description project.ext.description
                url 'https://github.com/caseif/Beret'

                scm {
                    url 'https://github.com/caseif/Beret'
                    connection 'scm:git:git://github.com/caseif/Beret.git'
                    developerConnection 'scm:git:git@github.com:caseif/Beret.git'
                }

                licenses {
                    license {
                        name 'MIT license'
                        url 'http://opensource.org/licenses/BSD-3-Clause'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
